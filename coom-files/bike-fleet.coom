product {
    num         0-2000  totalPrice
    // num         2000-2000  maxPrice
    1..1 Bike           bikes
}

structure Bike {
    num .#/kg	1-10000 totalWeight
    num .#/kg	5-5     maxWeight
	num .#/l	0-200   totalVolume
	num .#/l	80-80   requestedVolume
    num .#/€    0-2000  price
	Wheel			    frontWheel
	Wheel			    rearWheel
	Frame			    frame
	Carrier			    carrier
}

structure Carrier {
	0..3 Bag	        bags
}

structure Frame {
	0..2 Bag  	        bags
}

structure Bag {
    Capacity	        capacity
    num         0-180   price
}

enumeration Capacity {
	attribute num/l  volume
	attribute num/gr weight
    attribute num/€  price

	// B10  = ( 10,  200,   20 )
	B20  = ( 20,  450,   40 )
	B50  = ( 50,  800,   75 )
	B100 = (100, 1200,  130 )
}

enumeration Wheel {
	attribute	num/inch	size
	attribute	num/gr		weight
    attribute   num/€       price

	W26	= (	 26	    900	     95 )
	W28	= (	 28	   1000	    120 )
}


behavior {
    imply totalPrice = sum(bikes.price)

    // require totalPrice <= maxPrice
}

behavior Bike {
    require count(carrier.bags) + count(frame.bags) <= 4

    require frontWheel.size <= rearWheel.size

	imply totalWeight = frontWheel.weight + rearWheel.weight
						+ sum(carrier.bags.capacity.weight)
						+ sum(frame.bags.capacity.weight)

	require totalWeight <= maxWeight * 1000

	imply totalVolume = sum(carrier.bags.capacity.volume) + sum(frame.bags.capacity.volume)

	require totalVolume >= requestedVolume

    imply price = frontWheel.price + rearWheel.price + sum(carrier.bags.price) + sum(frame.bags.price)
}

behavior Bag {
    imply price = capacity.price
}
