product {
    1..1 Bike           bikes
}

structure Bike {
	Wheel			    frontWheel
	Wheel			    rearWheel
	Carrier			    carrier
	Color				color
}

structure Carrier {
	0..2 Bag	        bags
}

structure Bag {
    Capacity	        capacity
	Material	        material
}

enumeration Capacity {
	attribute num/l  volume

	B10  = ( 10 )
	B20  = ( 20 )
	B50  = ( 50 )
	B100 = (100 )
}

enumeration Material {
	Cotton
	Leather
	Polyester
}

enumeration Wheel {
	attribute	num/inch	size

	W25	= (	25 )
	W26	= (	26 )
	W27	= (	27 )
	W28	= (	28 )
}

enumeration Color {
	Red
	Silver
	Blue
}


behavior Bike {
    require frontWheel.size = rearWheel.size

	condition color = Red
	require frontWheel.size = 25
}

behavior Bag {
	condition material = Leather
	require capacity = B10
}
